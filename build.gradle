plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
}

import org.gradle.internal.os.OperatingSystem

def userHome = System.getProperty("user.home")
def hytaleHome = ""

if (project.hasProperty('hytale_home')) {
    hytaleHome = project.findProperty('hytale_home').toString()
} else {
    def os = OperatingSystem.current()
    if (os.isWindows()) {
        hytaleHome = "${userHome}/AppData/Roaming/Hytale"
    } else if (os.isMacOsX()) {
        hytaleHome = "${userHome}/Library/Application Support/Hytale"
    } else {
        hytaleHome = "${userHome}/.local/share/Hytale"
    }
}
hytaleHome = hytaleHome.replace('\\', '/')

group = 'br.tblack'
version = '1.1.0'

def patchline = (project.findProperty('patchline') ?: 'release').toString()
def gameDir = "${hytaleHome}/install/${patchline}/package/game/latest"
def serverJarPath = "${gameDir}/Server/HytaleServer.jar"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    compileOnly files(serverJarPath)
    shade files("libs/HyUI-0.8.8-2026-02-18.jar")
    compileOnly 'org.checkerframework:checker-qual:3.42.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['--enable-preview']
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set("")
    configurations = [project.configurations.shade]
}

tasks.register('runServer', JavaExec) {
    group = "hytale"
    description = "Launch the Hytale server with the mod loaded."

    dependsOn(shadowJar)

    standardInput = System.in

    classpath = files(serverJarPath)
    mainClass.set('com.hypixel.hytale.Main')

    def runDir = file("$projectDir/run")
    def modsDir = file("$runDir/run_mods")

    doFirst {
        if (!runDir.exists()) runDir.mkdirs()
        if (!modsDir.exists()) modsDir.mkdirs()

        modsDir.listFiles()?.each { it.delete() }

        copy {
            from(shadowJar.archiveFile)
            into(modsDir)
        }
        println "MOD JAR copied to: ${modsDir.absolutePath}"
    }

    workingDir = runDir

    def assetsPath = "${gameDir}/Assets.zip"

    args = [
            '--allow-op',
            '--disable-sentry',
            "--assets=${assetsPath}",
            "--mods=${modsDir.absolutePath}"
    ]

    jvmArgs = [
            '--enable-preview',
            '--enable-native-access=ALL-UNNAMED',
            '-Xmx2G',
            '-Dfile.encoding=UTF-8'
    ]
}