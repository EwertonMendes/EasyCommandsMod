plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'org.tblack'
version = (project.findProperty('version') ?: '0.1.1').toString()

def patchline = (project.findProperty('patchline') ?: 'release').toString()

def hytaleHome = project.findProperty('hytale_home')
if (hytaleHome == null || hytaleHome.toString().trim().isEmpty()) {
    def userHome = System.getProperty('user.home')
    hytaleHome = "${userHome}/AppData/Roaming/Hytale"
}
hytaleHome = hytaleHome.toString().replace('\\', '/')

def serverJar = file("${hytaleHome}/install/${patchline}/package/game/latest/Server/HytaleServer.jar")
if (!serverJar.exists()) {
    throw new GradleException("HytaleServer.jar not found at: ${serverJar.absolutePath.replace('\\','/')}")
}

def javaVer = (project.findProperty('java_version') ?: '25').toString().toInteger()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVer)
    }
}

repositories {
    mavenCentral()
}

configurations {
    shade
}

dependencies {
    compileOnly files(serverJar)
    compileOnly files("libs/HyUI-0.6.0-all.jar")
    shade files("libs/HyUI-0.6.0-all.jar")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    jvmArgs += ['--enable-preview']
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += ['--enable-preview']
}

jar {
    enabled = false
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set("")
    archiveVersion.set(project.version.toString())
    configurations = [project.configurations.shade]
}

tasks.build {
    dependsOn shadowJar
}
